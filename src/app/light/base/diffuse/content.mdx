
# 漫反射

## 概念

漫反射符合兰伯特定律，反射光线的强度与表面法线与光源方向之间的驾校的余弦值成正比

## 逐顶点渲染

计算核心放入 vert 中
<details>
<summary>代码</summary>
```shader
Shader "Note2/VertexLitDiffuse"
{
    Properties
    {
        _Color ("Main Color", Color) = (1,1,1,1)
        _MainTex ("Base (RGB)", 2D) = "white" {}
        _LightColor ("Light Color", Color) = (1,1,1,1)
    }
    SubShader
    {
        Tags { "RenderType"="Opaque" }
        LOD 200

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float2 uv : TEXCOORD0;
            };

            struct v2f
            {
                float4 pos : POSITION;
                float2 uv : TEXCOORD0;
                float3 color : COLOR;
            };

            sampler2D _MainTex;
            float4 _Color;
            float4 _LightColor;
            float4 _MainTex_ST;

            v2f vert (appdata v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.uv = v.uv * _MainTex_ST.xy + _MainTex_ST.zw;
                float3 normal = normalize(mul((float3x3)unity_WorldToObject, v.normal));
                float3 lightDir = normalize(_WorldSpaceLightPos0.xyz);
                float diff = max(0, dot(normal, lightDir));
                o.color = diff * _LightColor.rgb;
                return o;
            }

            half4 frag (v2f i) : SV_Target
            {
                half4 texColor = tex2D(_MainTex, i.uv) * _Color;
                texColor.rgb *= i.color;
                return texColor;
            }
            ENDCG
        }
    }
    FallBack "Diffuse"
}

```
</details>


![逐顶点渲染](https://minio.fzcode.com/unity/light/base/vertexLitDiffuse.png)


## 逐像素渲染

vert 中 计算了法线和光源，frag 中计算光照

<details>
<summary>代码</summary>
```shader
Shader "Note2/PixelLitDiffuse"
{
    Properties
    {
        _Color ("Main Color", Color) = (1,1,1,1)
        _MainTex ("Base (RGB)", 2D) = "white" {}
        _LightColor ("Light Color", Color) = (1,1,1,1)
    }
    SubShader
    {
        Tags { "RenderType"="Opaque" }
        LOD 200

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float2 uv : TEXCOORD0;
            };

            struct v2f
            {
                float4 pos : POSITION;
                float2 uv : TEXCOORD0;
                float3 normal : TEXCOORD1;
                float3 lightDir : TEXCOORD2;
            };

            sampler2D _MainTex;
            float4 _Color;
            float4 _LightColor;
            float4 _MainTex_ST;

            v2f vert (appdata v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.uv = v.uv * _MainTex_ST.xy + _MainTex_ST.zw;
                o.normal = normalize(mul((float3x3)unity_WorldToObject, v.normal));
                o.lightDir = normalize(_WorldSpaceLightPos0.xyz);

                return o;
            }

            half4 frag (v2f i) : SV_Target
            {
                float3 normal = normalize(i.normal);
                float3 lightDir = normalize(i.lightDir);
                float diff = max(0, dot(normal, lightDir));

                half4 texColor = tex2D(_MainTex, i.uv) * _Color;
                texColor.rgb *= diff * _LightColor.rgb;
                return texColor;
            }
            ENDCG
        }
    }
    FallBack "Diffuse"
}


```

</details>
![逐像素渲染](https://minio.fzcode.com/unity/light/base/diffuse.png)


## 对比

明显看出逐顶点渲染更加粗糙一些，在远处或者不重要的地方可以通过lod将逐像素渲染改为逐顶点渲染的，从而减少计算量。


## 半兰伯特 halfLambert

在一些情况下，无光照的背面部分为全黑，这是游戏开发中并不想看到的，所以value公司提出了半兰伯特模型

![半兰伯特](https://minio.fzcode.com/unity/light/base/Alyx_lambert_half_lambert.jpg)

<details>
<summary>代码</summary>
```shader
Shader "Note2/HalfLambert"
{
   Properties {
		_Diffuse ("Diffuse", Color) = (1, 1, 1, 1)
	}
	SubShader {
		Pass { 
			Tags { "LightMode"="ForwardBase" }
		
			CGPROGRAM
			
			#pragma vertex vert
			#pragma fragment frag
			
			#include "Lighting.cginc"
			
			fixed4 _Diffuse;
			
			struct a2v {
				float4 vertex : POSITION;
				float3 normal : NORMAL;
			};
			
			struct v2f {
				float4 pos : SV_POSITION;
				float3 worldNormal : TEXCOORD0;
			};
			
			v2f vert(a2v v) {
				v2f o;
				// Transform the vertex from object space to projection space
				o.pos = UnityObjectToClipPos(v.vertex);
				
				// Transform the normal from object space to world space
				o.worldNormal = mul(v.normal, (float3x3)unity_WorldToObject);
				
				return o;
			}
			
			fixed4 frag(v2f i) : SV_Target {
				// Get ambient term
				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;
				
				// Get the normal in world space
				fixed3 worldNormal = normalize(i.worldNormal);
				// Get the light direction in world space
				fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);
				
				// Compute diffuse term
				fixed halfLambert = dot(worldNormal, worldLightDir) * 0.5 + 0.5;
				fixed3 diffuse = _LightColor0.rgb * _Diffuse.rgb * halfLambert;
				
				fixed3 color = ambient + diffuse;
				
				return fixed4(color, 1.0);
			}
			
			ENDCG
		}
	} 
	FallBack "Diffuse"
}

```
</details>

![半兰伯特](https://minio.fzcode.com/unity/light/base/halfLambert.png)